      // Spike : appel dynamique a une procedure de service.

     h DftActGrp(*no) Option(*SrcStmt: *NoDebugIo)

      /copy RPGUNIT1,QZRUCLSP
      // DS pour les erreurs d'API.
      /copy RPGUNIT1,DSEC

       // Programme de service.
     d SrvPgm          s             10A   inz('AJOUTEUR')
     d SrvPgmLib       s             10A   inz('*LIBL')

       // Type de parametre pour l'appel de la procedure de service.
     d ParmType        s             10I 0 dim(1)
       // Valeur retour de QZRUCLSP.
     d RtnVal          s             10I 0

       // Message affiche a l'utilisateur.
     d message         s             50a   Varying
       // Variable numerique quelconque. Necessaire pour appeler la procedure.
     d x               s             10I 0

      /free

        x = 10;

        ParmType(1) = PARMTYPE_INT;

        QZRUCLSP( SrvPgm + SrvPgmLib:
                  'AJOUTE_UN' + x'00':
                  RETTYPE_INT:
                  ParmType: 1:
                  dsEC:
                  RtnVal:
                  x);

        message = %char(x) + ' + 1 = ' + %char(RtnVal);

        dsply message;

        *inlr = *on;

      /end-free

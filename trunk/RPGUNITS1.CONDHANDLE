
     D fail            pr

     D myHandler       pr
     D  C_CTOK                             Const LikeDs(CondTok)
     D  token                          *   Const
     D  result_code                  10i 0
     D  new_condition                      LikeDs(CondTok)

     D CEEHDLR         pr
     D  pCondHdlr                      *   ProcPtr
     D  commArea                       *   Const
     D  Feedback                     12a   Options(*Omit)

     D CondTok         ds                  Qualified Based(@)
     D  MsgSev                        5I 0
     D  MsgNo                         2A
     D                                1A
     D  MsgPrefix                     3A
     D  MsgKey                        4A

     D TheToken        s             40a   Inz('Valeur initiale') NoOpt
     D myHandlerPtr    s               *   ProcPtr

      /free

        myHandlerPtr = %paddr(myHandler);
        CEEHDLR(myHandlerPtr: %addr(TheToken): *Omit);

        fail();

        dsply TheToken;

        *inlr = *on;

      /end-free

     P myHandler       b
     D myHandler       pi
     D  C_CTOK                             Const LikeDs(CondTok)
     D  token                          *   Const
     D  result_code                  10i 0
     D  new_condition                      LikeDs(CondTok)

     D tokenContents   s                   Like(TheToken)
     D                                     Based(token)

      /free

        tokenContents = 'myHandler a ete appele.';
        result_code = 10;    // Resume.

      /end-free
     P myHandler       e

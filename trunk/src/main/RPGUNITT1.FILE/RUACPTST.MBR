      //
      // Acceptance Tests for RPGUnit.
      //

     H NoMain Option(*SrcStmt)


      //----------------------------------------------------------------------
      //   Exported Procedures
      //----------------------------------------------------------------------

     DsetUpSuite       pr
     DsetUp            pr

     DtestEmptyTest    pr
     DtestSuccess      pr
     DtestFailures     pr
     DtestNonTestProc  pr
     DtestSetupAndTearDown...
     D                 pr
     DtestError        pr
     DtestManyTests    pr
     DtestStackTrace   pr
     DtestMissingObject...
     D                 pr
     DtestErrorDuringSetup...
     D                 pr
     DtestErrorDuringTearDown...
     D                 pr
     DtestNoTestCase   pr
     DtestSetupSuiteAndTearDownSuite...
     D                 pr
     DtestBigInteger   pr
     DtestForceSpoolOnSuccess...
     D                 pr
     DtestReverseOrder...
     D                 pr
     DtestChooseTest   pr
     DtestChooseNonExistingTest...
     D                 pr


      //----------------------------------------------------------------------
      //   Imported Procedures
      //----------------------------------------------------------------------

      /copy RPGUNITY1,DSEC
      /copy RPGUNITY1,QGYCLST
      /copy RPGUNITY1,QGYGTLE
      /copy RPGUNITY1,QGYOLSPL
      /copy RPGUNITY1,QMHRCVPM
      /copy RPGUNITY1,OPNLIST
      /copy RPGUNITY1,QSPCLOSP
      /copy RPGUNITY1,QSPGETSP
      /copy RPGUNITY1,QSPOPNSP
      /copy RPGUNITY1,QWCRTVCA
      /copy RPGUNITY1,TEMPLATES
      /copy RPGUNITY1,USRSPC
      /copy RPGUNIT1,TESTCASE

     Dqcmdexc          pr                  ExtPgm('QCMDEXC')
     D cmd                         1024a   Const Options(*VarSize)
     D len                           15p 5 Const


      //----------------------------------------------------------------------
      //   Private Procedures
      //----------------------------------------------------------------------

     DbufEqual         pr              *
     D expBuf                       256a   Const Varying
     D actBuf_p                        *   Const

     DcloSplf          pr

     DfindInSpool      pr              *
     D charToFind                     1a   Const
     D startPos                        *   Const
     D endPos                          *   Const

     DopnSplf          pr
     D splfNm                        10a   Const

     DrtvJobUsrNm      pr                  Like(UsrNm_t)

     DrtvLatestSplf    pr                  LikeDs(SplfId_t)
     D usrNm                               Const Like(UsrNm_t)

     DrtvMsg           pr           256a
     D msgType                       10a   Const

     DrtvSplfHdl       pr            10i 0
     D splfNm                        10a   Const

     Drun              pr
     D cmd                        32767a   Const Varying
     D errorExpected                   n   Const Options(*NoPass)
     D msgType                       10a   Const Options(*NoPass)
     D msgTxt                       256a   Options(*NoPass)

     DsplfLineStartsWith...
     D                 pr
     D expString                    256a   Const Varying


      //----------------------------------------------------------------------
      //   Constants
      //----------------------------------------------------------------------

       // Constants for the 'run' procedure.
     D errorExpected   c                   Const(*on)
     D noError         c                   Const(*off)

       // SNA Character String (in spool files).
       // - New Line
     D NL              c                   Const(x'15')


      //----------------------------------------------------------------------
      //   Templates
      //----------------------------------------------------------------------

     D SplfId_t        ds                  Qualified Based(template)
     D  qlfJobNm                           LikeDs(QlfJobNm_t)
     D  nm                           10a
     D  nb                           10i 0


      //----------------------------------------------------------------------
      //   Global Variables
      //----------------------------------------------------------------------

       // Spool file.
     D splf            ds                  Qualified
         // - Handle
     D   hdl                         10i 0
         // - Print Data User Space Current Position Pointer
     D   bufPos_p                      *
         // - Print Data User Space End Pointer
     D   usrSpcEnd_p                   *

       // Message text.
     D msgTxt          s            256a


      //----------------------------------------------------------------------
      //   Test Procedures
      //----------------------------------------------------------------------

     PsetUpSuite       b                   Export
     DsetUpSuite       pi
      /free

        run( 'RUCRTTST PGM(QTEMP/TESTPGM01) SRCFILE(RPGUNITF1)' );
        run( 'RUCRTTST PGM(QTEMP/TESTPGM02) SRCFILE(RPGUNITF1)' );
        run( 'RUCRTTST PGM(QTEMP/TESTPGM03) SRCFILE(RPGUNITF1)' );
        run( 'RUCRTTST PGM(QTEMP/TESTPGM04) SRCFILE(RPGUNITF1)' );
        run( 'RUCRTTST PGM(QTEMP/TESTPGM05) SRCFILE(RPGUNITF1)' );
        run( 'RUCRTTST PGM(QTEMP/TESTPGM06) SRCFILE(RPGUNITF1)' );
        run( 'RUCRTTST PGM(QTEMP/TESTPGM07) SRCFILE(RPGUNITF1)' );
        callp(e) run( 'DLTSRVPGM QTEMP/TESTPGM08' );    // No TESTPGM08.
        run( 'RUCRTTST PGM(QTEMP/TESTPGM09) SRCFILE(RPGUNITF1)' );
        run( 'RUCRTTST PGM(QTEMP/TESTPGM10) SRCFILE(RPGUNITF1)' );
        run( 'RUCRTTST PGM(QTEMP/TESTPGM11) SRCFILE(RPGUNITF1)' );
        run( 'RUCRTTST PGM(QTEMP/TESTPGM12) SRCFILE(RPGUNITF1)' );
        run( 'RUCRTTST PGM(QTEMP/TESTPGM13) SRCFILE(RPGUNITF1)' );
        run( 'RUCRTTST PGM(QTEMP/TESTPGM14) SRCFILE(RPGUNITF1)' );
        run( 'RUCRTTST PGM(QTEMP/TESTPGM15) SRCFILE(RPGUNITF1)' );
        run( 'RUCRTTST PGM(QTEMP/TESTPGM16) SRCFILE(RPGUNITF1)' );

      /end-free
     P                 e


     PsetUp            b                   Export
     DsetUp            pi
      /free

        clear msgTxt;
        clear splf;

      /end-free
     P                 e


     PtestEmptyTest    b                   Export
     DtestEmptyTest    pi
      /free

        run( 'RUCALLTST QTEMP/TESTPGM01': noError: '*COMP': msgTxt );

        aEqual( 'Success. 1 test cases, 0 assertions, 0 failures, 0 errors.':
                msgTxt );

      /end-free
     P                 e


     PtestSuccess      b                   Export
     DtestSuccess      pi
      /free

        run( 'RUCALLTST QTEMP/TESTPGM02': noError: '*COMP': msgTxt );

        aEqual( 'Success. 1 test cases, 1 assertions, 0 failures, 0 errors.':
                msgTxt );

      /end-free
     P                 e


     PtestFailures     b                   Export
     DtestFailures     pi
      /free

        run( 'RUCALLTST QTEMP/TESTPGM03': errorExpected: '*ESCAPE': msgTxt );

        aEqual( 'FAILURE! 2 test cases, 2 assertions, 2 failures, 0 errors.':
                msgTxt );

        opnSplf( 'QSYSPRT' );
        splfLineStartsWith( '*** Tests from TESTPGM03 ***' );
        splfLineStartsWith( 'TESTFALSE - FAILURE' );
        splfLineStartsWith( 'Expected 5, but was 4.' );
        splfLineStartsWith( '  assert' );
        splfLineStartsWith( '  iEqual' );
        splfLineStartsWith( '  TESTFALSE (TESTPGM03:1300)' );
        splfLineStartsWith( '-----------------------' );
        splfLineStartsWith( 'TESTFALSE2 - FAILURE' );
        splfLineStartsWith( 'Expected 6, but was 4.' );
        splfLineStartsWith( '  assert' );
        splfLineStartsWith( '  iEqual' );
        splfLineStartsWith( '  TESTFALSE2 (TESTPGM03:2200)' );
        splfLineStartsWith( '-----------------------' );
        splfLineStartsWith( 'FAILURE! 2 test cases, 2 assertions,'
                          + ' 2 failures, 0 errors.' );
        cloSplf();

      /end-free
     P                 e


     PtestNonTestProc...
     P                 b                   Export
     DtestNonTestProc...
     D                 pi
      /free

        run( 'RUCALLTST QTEMP/TESTPGM04': noError: '*COMP': msgTxt );

        aEqual( 'Success. 2 test cases, 0 assertions, 0 failures, 0 errors.':
                msgTxt );

      /end-free
     P                 e


     PtestSetupAndTearDown...
     P                 b                   Export
     DtestSetupAndTearDown...
     D                 pi
      /free

        run( 'RUCALLTST QTEMP/TESTPGM05': errorExpected: '*ESCAPE': msgTxt );

        aEqual( 'FAILURE! 2 test cases, 0 assertions, 1 failures, 0 errors.':
                msgTxt );

        aEqual( 'setup'        : rtvMsg('*INFO') );
        aEqual( 'test1'        : rtvMsg('*INFO') );
        aEqual( 'teardown'     : rtvMsg('*INFO') );
        aEqual( 'setup'        : rtvMsg('*INFO') );
        aEqual( 'test2'        : rtvMsg('*INFO') );    // Raises a failure.
        aEqual( 'teardown'     : rtvMsg('*INFO') );    // Call 'teardown' anyway.

      /end-free
     P                 e


     PtestError        b                   Export
     DtestError        pi
      /free

        run( 'RUCALLTST QTEMP/TESTPGM06': errorExpected: '*ESCAPE': msgTxt );

        aEqual( 'ERROR! 1 test cases, 0 assertions, 0 failures, 1 errors.':
                msgTxt );

        opnSplf( 'QSYSPRT' );
        splfLineStartsWith( '*** Tests from TESTPGM06 ***' );
        splfLineStartsWith( 'TESTERROR - ERROR' );
        splfLineStartsWith( 'MCH1211 - ' );
        splfLineStartsWith( '  TESTERROR (TESTPGM06:1400)' );
        splfLineStartsWith( '-----------------------' );
        splfLineStartsWith( 'ERROR! 1 test cases, 0 assertions,'
                          + ' 0 failures, 1 errors.' );
        cloSplf();

      /end-free
     P                 e


     PtestManyTests    b                   Export
     DtestManyTests    pi
      /free

        run( 'RUCALLTST QTEMP/TESTPGM13': noError: '*COMP': msgTxt );

        aEqual( 'Success. 256 test cases, 0 assertions, 0 failures, 0 errors.':
                msgTxt );

      /end-free
     P                 e


     PtestStackTrace   b                   Export
     DtestStackTrace   pi
      /free

        run( 'RUCALLTST QTEMP/TESTPGM07' : errorExpected : '*ESCAPE': msgTxt );

        aEqual( 'FAILURE! 1 test cases, 1 assertions, 1 failures, 0 errors.':
                msgTxt );

        opnSplf( 'QSYSPRT' );
        splfLineStartsWith( '*** Tests from TESTPGM07 ***' );
        splfLineStartsWith( 'TESTSTACK - FAILURE' );
        splfLineStartsWith( 'Expected 5, but was 4.' );
        splfLineStartsWith( '  assert' );
        splfLineStartsWith( '  iEqual' );
        splfLineStartsWith( '  FAILINGPROC (TESTPGM07:1300)' );
        splfLineStartsWith( '  TESTSTACK (TESTPGM07:2200)' );
        splfLineStartsWith( '-----------------------' );
        splfLineStartsWith( 'FAILURE! 1 test cases, 1 assertions,'
                          + ' 1 failures, 0 errors.' );
        cloSplf();

      /end-free
     P                 e


     PtestMissingObject...
     P                 b                   Export
     DtestMissingObject...
     D                 pi
      /free

        run( 'RUCALLTST QTEMP/TESTPGM08' : errorExpected : '*ESCAPE' : msgTxt );

        aEqual( 'RPGUnit Error.'
              + ' Error while loading the test suite in QTEMP/TESTPGM08.' :
                msgTxt );

      /end-free
     P                 e


     PtestErrorDuringSetup...
     P                 b                   Export
     DtestErrorDuringSetup...
     D                 pi
      /free

        run( 'RUCALLTST QTEMP/TESTPGM09' : errorExpected : '*ESCAPE' : msgTxt );

        aEqual( 'ERROR! 1 test cases, 0 assertions, 0 failures, 1 errors.' :
                msgTxt );

        aEqual( 'setup'        : rtvMsg('*INFO') );    // Error during setup.
              // Test should not be called.
        aEqual( 'teardown'     : rtvMsg('*INFO') );    // Teardown should be called anyway.

        opnSplf( 'QSYSPRT' );
        splfLineStartsWith( '*** Tests from TESTPGM09 ***' );
        splfLineStartsWith( 'TEST - ERROR' );
        splfLineStartsWith( 'MCH1211 - ' );
        splfLineStartsWith( '  SETUP (TESTPGM09:2100)' );
        splfLineStartsWith( '-----------------------' );
        splfLineStartsWith( 'ERROR! 1 test cases, 0 assertions,'
                          + ' 0 failures, 1 errors.' );
        cloSplf();

      /end-free
     P                 e


     PtestErrorDuringTearDown...
     P                 b                   Export
     DtestErrorDuringTearDown...
     D                 pi
      /free

        run( 'RUCALLTST QTEMP/TESTPGM10' : errorExpected : '*ESCAPE' : msgTxt );

        aEqual( 'ERROR! 1 test cases, 0 assertions, 0 failures, 1 errors.' :
                msgTxt );

        opnSplf( 'QSYSPRT' );
        splfLineStartsWith( '*** Tests from TESTPGM10 ***' );
        splfLineStartsWith( 'TEST - ERROR' );
        splfLineStartsWith( 'MCH1211 - ' );
        splfLineStartsWith( '  TEARDOWN (TESTPGM10:1900)' );
        splfLineStartsWith( '-----------------------' );
        splfLineStartsWith( 'ERROR! 1 test cases, 0 assertions,'
                          + ' 0 failures, 1 errors.' );
        cloSplf();

      /end-free
     P                 e


     PtestNoTestCase   b                   Export
     DtestNoTestCase   pi
      /free

        run( 'RUCALLTST QTEMP/TESTPGM11' : errorExpected : '*ESCAPE' : msgTxt );

        aEqual( 'RPGUnit Error.'
              + ' No test case found in service program QTEMP/TESTPGM11.' :
                msgTxt );

      /end-free
     P                 e


     PtestSetupSuiteAndTearDownSuite...
     P                 b                   Export
     DtestSetupSuiteAndTearDownSuite...
     D                 pi
      /free

        run( 'RUCALLTST QTEMP/TESTPGM12': errorExpected: '*ESCAPE': msgTxt );

        aEqual( 'FAILURE! 2 test cases, 0 assertions, 1 failures, 0 errors.':
                msgTxt );

        aEqual( 'setupSuite'   : rtvMsg('*INFO') );
        aEqual( 'setup'        : rtvMsg('*INFO') );
        aEqual( 'test1'        : rtvMsg('*INFO') );
        aEqual( 'teardown'     : rtvMsg('*INFO') );
        aEqual( 'setup'        : rtvMsg('*INFO') );
        aEqual( 'test2'        : rtvMsg('*INFO') );    // Raises a failure.
        aEqual( 'teardown'     : rtvMsg('*INFO') );    // Call 'teardown' anyway.
        aEqual( 'teardownSuite': rtvMsg('*INFO') );    // Call 'teardownsuite' anyway.

      /end-free
     P                 e


     PtestBigInteger   b                   Export
     DtestBigInteger   pi
      /free

        run( 'RUCALLTST QTEMP/TESTPGM14' : errorExpected : '*ESCAPE' : msgTxt );

        aEqual( 'FAILURE! 2 test cases, 2 assertions, 1 failures, 0 errors.' :
                msgTxt );

        opnSplf( 'QSYSPRT' );
        splfLineStartsWith( '*** Tests from TESTPGM14 ***' );
        splfLineStartsWith( 'TEST_DIFFERENT - FAILURE' );
        splfLineStartsWith( 'Expected 1234567890123456789012345678901,'
                          + ' but was 123456789012345678901234567890' );
        splfLineStartsWith( '0.' );
        splfLineStartsWith( '  assert' );
        splfLineStartsWith( '  iEqual' );
        splfLineStartsWith( '  TEST_DIFFERENT (TESTPGM14:2800)' );
        splfLineStartsWith( '-----------------------' );
        splfLineStartsWith( 'FAILURE! 2 test cases, 2 assertions,'
                          + ' 1 failures, 0 errors.' );
        cloSplf();

      /end-free
     P                 e


     PtestForceSpoolOnSuccess...
     P                 b                   Export
     DtestForceSpoolOnSuccess...
     D                 pi
      /free

        run( 'RUCALLTST TSTPGM(QTEMP/TESTPGM15) DETAIL(*ALL)' :
             noError :
             '*COMP' :
             msgTxt );

        aEqual( 'Success. 2 test cases, 3 assertions, 0 failures, 0 errors.' :
                msgTxt );

        opnSplf( 'QSYSPRT' );
        splfLineStartsWith( '*** Tests from TESTPGM15 ***' );
        splfLineStartsWith( 'TESTTRUE - Success' );
        splfLineStartsWith( '  1 assertions' );
        splfLineStartsWith( '-----------------------' );
        splfLineStartsWith( 'TESTTRUE2 - Success' );
        splfLineStartsWith( '  2 assertions' );
        splfLineStartsWith( '-----------------------' );
        splfLineStartsWith( 'Success. 2 test cases, 3 assertions,'
                          + ' 0 failures, 0 errors.' );
        cloSplf();

      /end-free
     P                 e


     P testReverseOrder...
     P                 b                   Export
     D testReverseOrder...
     D                 pi
      /free

        run( 'RUCALLTST TSTPGM(QTEMP/TESTPGM16) ORDER(*REVERSE)' :
             noError :
             '*COMP' :
             msgTxt );

        aEqual( 'Success. 2 test cases, 0 assertions, 0 failures, 0 errors.':
                msgTxt );

        aEqual( 'setup'        : rtvMsg('*INFO') );
        aEqual( 'test2'        : rtvMsg('*INFO') );
        aEqual( 'teardown'     : rtvMsg('*INFO') );
        aEqual( 'setup'        : rtvMsg('*INFO') );
        aEqual( 'test1'        : rtvMsg('*INFO') );
        aEqual( 'teardown'     : rtvMsg('*INFO') );

      /end-free
     P                 e


     PtestChooseTest   b                   Export
     DtestChooseTest   pi
      /free

        run( 'RUCALLTST QTEMP/TESTPGM05 TSTPRC(TEST2)' :
             errorExpected : '*ESCAPE' : msgTxt );

        aEqual( 'FAILURE! 1 test cases, 0 assertions, 1 failures, 0 errors.':
                msgTxt );

        aEqual( 'setup'        : rtvMsg('*INFO') );
        aEqual( 'test2'        : rtvMsg('*INFO') );
        aEqual( 'teardown'     : rtvMsg('*INFO') );

        opnSplf( 'QSYSPRT' );
        splfLineStartsWith( '*** Tests from TESTPGM05 ***' );
        splfLineStartsWith( 'TEST2 - FAILURE' );
        splfLineStartsWith( 'test2' );
        splfLineStartsWith( '  TEST2 (TESTPGM05' );
        splfLineStartsWith( '-----------------------' );
        splfLineStartsWith( 'FAILURE! 1 test cases, 0 assertions,'
                          + ' 1 failures, 0 errors.' );
        cloSplf();

      /end-free
     P                 e


     PtestChooseNonExistingTest...
     P                 b                   Export
     DtestChooseNonExistingTest...
     D                 pi
      /free

        run( 'RUCALLTST QTEMP/TESTPGM05 TSTPRC(NON_EXISTING_TEST)' :
             errorExpected : '*ESCAPE' : msgTxt );

        aEqual( 'ERROR! 0 test cases, 0 assertions, 0 failures, 1 errors.':
                msgTxt );

        opnSplf( 'QSYSPRT' );
        splfLineStartsWith( '*** Tests from TESTPGM05 ***' );
        splfLineStartsWith( 'NON_EXISTING_TEST - ERROR' );
        splfLineStartsWith( 'Test procedure not found.' );
        splfLineStartsWith( '-----------------------' );
        splfLineStartsWith( 'ERROR! 0 test cases, 0 assertions,'
                          + ' 0 failures, 1 errors.' );
        cloSplf();

      /end-free
     P                 e


      //----------------------------------------------------------------------
      //   Helper Procedures
      //----------------------------------------------------------------------

     PbufEqual         b
     DbufEqual         pi              *
     D expBuf                       256a   Const Varying
     D actBuf_p                        *   Const

       // Actual buffer contents.
     D actBuf          s            256a   Based(actBuf_p)

      /free

        aEqual( expBuf :
                %subst( actBuf : 1 : %len(expBuf) ) );

        return actBuf_p + %len(expBuf);

      /end-free
     P                 e


     PcloSplf          b
     DcloSplf          pi
      /free

        QSPCLOSP( splf.hdl : percolateErrors );

      /end-free
     P                 e


     PfindInSpool      b
     DfindInSpool      pi              *
     D charToFind                     1a   Const
     D startPos                        *   Const
     D endPos                          *   Const

       // Character at current position.
     D currentChar_p   s               *
     D currentChar     s              1a   Based(currentChar_p)

      /free

        currentChar_p = startPos;

        dow currentChar_p <= endPos;
          if currentChar = charToFind;
            return currentChar_p;
          endif;
          currentChar_p += 1;
        enddo;

        fail( 'Character "' + charToFind + '" not found in buffer' );

      /end-free
     P                 e


     PopnSplf          b
     DopnSplf          pi
     D splfNm                        10a   Const

       // QSP API generic header.
     D qspGenHdr_p     s               *
     D qspGenHdr       ds                  LikeDs(qspGenHdr_t)
     D                                     Based(qspGenHdr_p)
       // User Space to store spool contents.
     D usrSpcNm        ds                  LikeDs(Object_t)

      /free

        splf.hdl = rtvSplfHdl( splfNm );

        usrSpcNm.nm  = 'RUSPOOL';
        usrSpcNm.lib = 'QTEMP';

        qspGenHdr_p = crtUsrSpc( usrSpcNm :
                                 'RPGUnit - SelfTest - Spool File Contents.' );

        QSPGETSP( splf.hdl :
                  usrSpcNm :
                  'SPFR0300' :
                  -1 :    // Reading an entire spooled file.
                  '*ERROR' :
                  percolateErrors );

        aEqual( 'C' : qspGenHdr.compInd );

        splf.bufPos_p    = qspGenHdr_p + qspGenHdr.firstBufOff;
        splf.usrSpcEnd_p = qspGenHdr_p + qspGenHdr.usrSpcSizeUsed;

      /end-free
     P                 e


     PrtvJobUsrNm      b
     DrtvJobUsrNm      pi            10a

       // Job attributes.
     D jobAttribs      ds                  LikeDs(dsRTVC0100)
       // Job attribute keys.
     D attribKeys      s             10i 0 Dim(1)
       // Current user profile name job attribute entry.
     D usrNmEnt        ds                  LikeDs(dsRTVC0100Ent)

      /free

        attribKeys(1) = CUR_USR_NM;

        QWCRTVCA( jobAttribs :
                  %size(jobAttribs) :
                  'RTVC0100' :
                  %elem(attribKeys) :
                  attribKeys :
                  percolateErrors );

        if (jobAttribs.attribCnt <> 1);
          fail( 'QWCRTVCA returned an unexpected number of job attributes' );
        endif;

        usrNmEnt = jobAttribs.attribEnts;

        if (usrNmEnt.key <> CUR_USR_NM) or
           (usrNmEnt.dataType <> 'C') or
           (usrNmEnt.dataLen <> 10);
          fail( 'QWCRTVA returned an unexpected attribute format' );
        endif;

        return usrNmEnt.data;

      /end-free
     P                 e


     PrtvLatestSplf    b
     DrtvLatestSplf    pi                  LikeDs(SplfId_t)
     D usrNm                               Const Like(UsrNm_t)

       // Retrieve only one record.
     D ONE_RECORD      c                   Const(1)
       // Retrieve spool files from all jobs.
     D ALL_JOBS        c                   Const(*blank)
       // Do not retrieve any record.
     D NO_RETRIEVAL    c                   Const(*zero)
       // The entire list is build synchronously.
     D BUILD_ENTIRE_LIST_SYNCHRONOUSLY...
     D                 c                   Const(-1)
       // Latest spool file id.
     D latestSplfId    ds                  LikeDs(SplfId_t)
       // Latest spool file info from QGYOLSPL API.
     D latestSplfInfo  ds                  LikeDs(dsOSPL0300)
       // Open list information for QGYOLSPL API.
     D listInfo        ds                  LikeDs(dsOpnList)
       // Open list handle.
     D opnListHdl      s                   Like(dsOpnList.rqsHdl)
       // Last spool file record index.
     D lastRcdIdx      s             10i 0
       // Spool file sorting information.
     D sortInfo        ds                  LikeDs(SortInfo_t)
       // Spool file filtering information, according to OSPF0100 format.
     D filterInfo      ds                  Qualified
     D  usrNmCnt                     10i 0 Inz(1)
     D   usrNm                       10a
     D                                2a   Inz(x'00')
     D  outqNmCnt                    10i 0 Inz(1)
     D   outqNm                      10a   Inz('*ALL')
     D   outqLibNm                   10a   Inz(*blank)
     D  formType                     10a   Inz('*ALL')
     D  usrData                      10a   Inz('RUCMDRUN')
     D  statusCnt                    10i 0 Inz(1)
     D   splfStatus                  10a   Inz('*READY')
     D                                2a   Inz(x'00')
     D  deviceNmCnt                  10i 0 Inz(1)
     D   deviceNm                    10a   Inz('*ALL')
     D                                2a   Inz(x'00')

      /free

        filterInfo.usrNm = usrNm;

        sortInfo = *allx'00';

        QGYOLSPL( latestSplfInfo :
                  NO_RETRIEVAL :
                  listInfo :
                  BUILD_ENTIRE_LIST_SYNCHRONOUSLY :
                  sortInfo :
                  filterInfo :
                  ALL_JOBS :
                  'OSPL0300' :
                  percolateErrors );

        if ( listInfo.retRcdCnt <> 0 );
          fail( 'QGYOLSPL unexpectedly returned a record' );
        endif;

        if ( listInfo.infoCompInd <> 'C' );
          fail( 'QGYOLSPL could not retrieve complete info. ' +
                'Indicator=' + listInfo.infoCompInd );
        endif;

        if ( listInfo.firstRcdIdx <> 0 );
          fail( 'QGYOLSPL unexpectedly returned a first record' );
        endif;

        assert( listInfo.totalRcdCnt > 0 :
                'No spool file found for user ' + usrNm );

        opnListHdl = listInfo.rqsHdl;
        lastRcdIdx = listInfo.totalRcdCnt;

        QGYGTLE( latestSplfInfo :
                 %size(latestSplfInfo) :
                 opnListHdl :
                 listInfo :
                 ONE_RECORD :
                 lastRcdIdx :
                 percolateErrors );

        QGYCLST( listInfo.rqsHdl : percolateErrors );

        latestSplfId.qlfJobNm.jobNm = latestSplfInfo.jobNm;
        latestSplfId.qlfJobNm.usrNm = latestSplfInfo.usrNm;
        latestSplfId.qlfJobNm.jobNb = latestSplfInfo.jobNb;
        latestSplfId.nm             = latestSplfInfo.splfNm;
        latestSplfId.nb             = latestSplfInfo.splfNb;

        return latestSplfId;

      /end-free
     P                 e


     PrtvMsg           b
     DrtvMsg           pi           256a
     D msgType                       10a   Const

     D fmtNm           c                   Const('RCVM0200')
     D callStkEnt      c                   Const('*')
     D callStkCnt      c                   Const(1)
     D msgKey          c                   Const(*blank)
     D waitTime        c                   Const(0)
     D msgAction       c                   Const('*REMOVE')

     D rawMsgInfo      ds                  LikeDs(RCVM0200Hdr)
     D                                     Based(rawMsgInfo_p)
     D rawMsgInfo_p    s               *
     D rawMsgInfoBuf   s          32565a
     D msg             s            256a   Based(msg_p)
     D msg_p           s               *

      /free

        rawMsgInfo_p = %addr( rawMsgInfoBuf );

        QMHRCVPM( rawMsgInfoBuf :
                  %size(rawMsgInfoBuf) :
                  fmtNm :
                  callStkEnt :
                  callStkCnt :
                  msgType :
                  msgKey :
                  waitTime :
                  msgAction :
                  percolateErrors );

        assert( rawMsgInfo.bytesA > 0 : %trim(msgType) + ' message not found' );

        if rawMsgInfo.msgLenR > 0;    // If there is a Message, return it.
          msg_p = %addr(rawMsgInfo)
                + %size(rawMsgInfo)
                + rawMsgInfo.rplDataLenR;
          return %subst( msg : 1 : rawMsgInfo.msgLenR );
        else;    // Else return 'Replacement data or impromptu message text'.
          msg_p = %addr(rawMsgInfo) + %size(rawMsgInfo) + 0;
          return %subst( msg : 1 : rawMsgInfo.rplDataLenR );
        endif;

      /end-free
     P                 e


     PrtvSplfHdl       b
     DrtvSplfHdl       pi            10i 0
     D splfNm                        10a   Const

       // Latest spool file handle.
     D latestSplfHdl   s             10i 0
       // Latest spool file id.
     D latestSplfId    ds                  LikeDs(SplfId_t)
       // Current user profile name.
     D curUsrNm        s                   Like(UsrNm_t)

      /free

        // A simple QSPOPNSP( splfHdl : '*' : *blank : *blank : splfNm : -1 : 1 : percolateErrors )
        // would be enough for regular (i.e., interactive and submitted) jobs, but it does
        // not work for remote commands (a.k.a. server jobs).  In the latter, the spool file
        // is created by a special job named QPRTJOB.

        curUsrNm = rtvJobUsrNm();
        latestSplfId = rtvLatestSplf( curUsrNm );

        aEqual( splfNm : latestSplfId.nm );    // Sanity check.

        QSPOPNSP( latestSplfHdl :
                  latestSplfId.qlfJobNm :
                  *blank :
                  *blank :
                  latestSplfId.nm :
                  latestSplfId.nb :
                  1 :
                  percolateErrors );

        return latestSplfHdl;

      /end-free
     P                 e


     Prun              b
     Drun              pi
     D cmd                        32767a   Const Varying
     D errorExpected                   n   Const Options(*NoPass)
     D msgType                       10a   Const Options(*NoPass)
     D msgTxt                       256a   Options(*NoPass)

      /free

        if %parms >= 2 and errorExpected;
          callp(e) qcmdexc( cmd: %len(cmd) );
          assert( %error: 'Expected error missing' );
        else;
          qcmdexc( cmd: %len(cmd) );
        endif;

        if %parms >= 4;
          msgTxt = rtvMsg( msgType );
        endif;

      /end-free
     P                 e


     PsplfLineStartsWith...
     P                 b
     DsplfLineStartsWith...
     D                 pi
     D expString                    256a   Const Varying

      /free

        splf.bufPos_p = findInSpool( NL : splf.bufPos_p : splf.usrSpcEnd_p );
        splf.bufPos_p = bufEqual( expString : splf.bufPos_p + 1 );

      /end-free
     P                 e


